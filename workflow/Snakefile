
# set rule order for different cis results processing scripts
ruleorder: annotate_cis_results_dctap > annotate_cis_results_encode

configfile: "config/config.yml"
include: "rules/compute_heldout_indirect_effects_rate.smk"
include: "rules/calculate_direct_vs_effects.smk"

# perform all analyses
rule all:
  input:
    expand("results/{sample}/analyze_indirect_effects.html", sample = config["datasets"]),
    expand("results/{type}_positive_hit_rates.tsv", type = ["cis", "trans"]),
    expand("results/annotated_crispr_data/EPCrisprBenchmark_{dataset}_element_classes_direct_effects.tsv.gz",
           dataset = config["crispr_benchmark_files"])

# combine positive hit rates of held-out and training datasets
rule combine_positive_hit_rates:
  input:
    cis_rates_heldout = expand("results/{sample}/cis_positive_rate.tsv", sample = config["datasets"]),
    trans_rates_heldout = expand("results/{sample}/trans_positive_rate.tsv", sample = config["datasets"]),
    cis_rates_training = config["training_dir"] + "/Gasperini2019/trans_effects/cis_positive_rate_0.13gStd_MAST_perCRE_GRCh38.tsv",
    trans_rates_training = config["training_dir"] + "/Gasperini2019/trans_effects/trans_positive_rate_0.13gStd_MAST_perCRE_GRCh38.tsv"
  output:
    cis_rates = "results/cis_positive_hit_rates.tsv",
    trans_rates = "results/trans_positive_hit_rates.tsv"
  conda: "sceptre"
  script:
    "scripts/combine_positive_hit_rates.R"
